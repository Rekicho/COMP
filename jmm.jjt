options
{
	LOOKAHEAD=1;
	MULTI=true;
}

PARSER_BEGIN(jmm)

import java.io.*;

public class jmm {
	static int errors = 0;

	public static void printUsage() {
		System.out.println("Usage:  ");
		System.out.println("       java jmm <input_file.jmm>");
	}

	public static void main(String args[]) throws ParseException {
		if(args.length != 1) {
			printUsage();
			return;
		}
			
		FileInputStream file;
		try {
			file = new FileInputStream(args[0]);
		} catch (Exception e) {
			System.err.println("File " + args[0] + " not found");
			return;
		}
		
		jmm myjmm = new jmm(file);
		SimpleNode root = myjmm.Program(); // devolve referência para o nó raiz da árvore

		root.dump(""); // imprime no ecrã a árvore
	}
}

PARSER_END(jmm)

// WHITE SPACE
SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| "\f"
}

// COMMENTS
SPECIAL_TOKEN :
{
  <SINGLE_LINE_COMMENT: "//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
| <FORMAL_COMMENT: "/**" (~["*"])* "*" ("*" | (~["*","/"] (~["*"])* "*"))* "/">
| <MULTI_LINE_COMMENT: "/*" (~["*"])* "*" ("*" | (~["*","/"] (~["*"])* "*"))* "/">
}

// RESERVED
TOKEN :
{
  < BOOLEAN: "boolean" >
| < CLASS: "class" >
| < ELSE: "else" >
| < EXTENDS: "extends" >
| < FALSE: "false" >
| < IF: "if" >
| < INT: "int" >
| < LENGTH: "length" >
| < MAIN: "main" >
| < NEW: "new" >
| < PUBLIC: "public" >
| < RETURN: "return" >
| < STATIC: "static" >
| < STRING: "String" >
| < THIS: "this" >
| < TRUE: "true" >
| < VOID: "void" >
| < WHILE: "while" >
}

// IDENTIFIER AND INTEGER
TOKEN : 
{
  < IDENTIFIER : (["A"-"Z","a"-"z","$","_"]) (["A"-"Z","a"-"z","0"-"9","$","_"])* >
| < INTEGERLITERAL: (["1"-"9"] (["0"-"9"])*) | ("0" ["x","X"] (["0"-"9","a"-"f","A"-"F"])+) | ("0" (["0"-"7"])*) >
}

// SEPARATORS
TOKEN :
{
  < LPAREN: "(" >
| < RPAREN: ")" >
| < LBRACE: "{" >
| < RBRACE: "}" >
| < LBRACKET: "[" >
| < RBRACKET: "]" >
| < SEMICOLON: ";" >
| < COMMA: "," >
| < DOT: "." >
}

// OPERATORS
TOKEN : 
{
  < ASSIGN: "=" >
| < LT: "<" >
| < BANG: "!" >
| < SC_AND: "&&" >
| < PLUS: "+" >
| < MINUS: "-" >
| < STAR: "*" >
| < SLASH: "/" >
}

JAVACODE
void error_skipto(int kind) {
  ParseException e = generateParseException();  // generate the exception object.
  System.out.println("Error in while Statement:\n" + e.toString());  // print the error message
  Token t;
  do {
    t = getNextToken();
  } while (t.kind != kind);
    // The above loop consumes tokens all the way up to a token of
    // "kind".  We use a do-while loop rather than a while because the
    // current token is the one immediately before the erroneous token
    // (in our case the token immediately before what should have been
    // "if"/"while".
}


SimpleNode Program(): {}
{
	ClassDeclaration() <EOF>	  
	{return jjtThis;}
}

void ClassDeclaration(): {Token t1, t2;}
{
	try {
		"class" t1=<IDENTIFIER> {jjtThis.class_name = t1.image;} ["extends" t2=<IDENTIFIER> {jjtThis.extends_class = t2.image;}] "{" (VarDeclaration())* ("public" (MainDeclaration() | MethodDeclaration()))* "}" 
	} catch(Exception e) {
		System.out.println("Expected Class Declaration\n\n" + e);
		System.exit(1);
	}
}

void VarDeclaration(): {Token t;}
{
	try {
		Type() t=<IDENTIFIER> ";" {jjtThis.name = t.image;}
	} catch(Exception e) {
		System.out.println("Expected Var Declaration\n\n" + e);
		System.exit(1);
	}
}

void MainDeclaration(): {Token t;}
{
	try {
		"static" "void" "main" "(" "String" "[" "]" t=<IDENTIFIER> ")" "{" {jjtThis.args = t.image;}
		(LOOKAHEAD(2) VarDeclaration())* (LOOKAHEAD(2) Statement())* "}" 
	} catch(Exception e) {
		System.out.println("Expected Main Declaration\n\n" + e);
		System.exit(1);
	}
}

void MethodDeclaration(): {Token t;}
{
	try {
		Type() t=<IDENTIFIER> {jjtThis.name = t.image;} "(" [Parameters()] ")" "{" 	
		(LOOKAHEAD(2) VarDeclaration())* (LOOKAHEAD(2) Statement())* Return() "}"
	} catch(Exception e) {
		System.out.println("Expected Method Declaration\n\n" + e);
		System.exit(1);
	}
}

void Parameters(): {}
{
	Parameter() ("," Parameter())*
}

void Parameter(): {Token t;}
{
	Type() t=<IDENTIFIER> {jjtThis.name = t.image;}
}

void Return(): {}
{
	"return" Expression() ";"
}

void Type() : {Token t;}
{
	try{
		( t="int" ["[" "]" {jjtThis.isArray = true;}] 
		| t="boolean" 
		| t=<IDENTIFIER>) {jjtThis.name = t.image;}
	} catch(Exception e) {
		System.out.println("Expected Type\n\n" + e);
		System.exit(1);
	}
}

void Statement(): {Token t;}
{
	try {
		"{" (Statement())* "}"  {jjtThis.type = "multiple";}
		| "if" "(" Expression() ")" Statement() "else" Statement() {jjtThis.type = "if";}
		| "while" try {"(" Expression() ")"} catch(ParseException e) { error_skipto(RPAREN); errors++; if(errors == 10) System.exit(10);} Statement() {jjtThis.type = "while";}
		| LOOKAHEAD(<IDENTIFIER> "[") t=<IDENTIFIER> ArrayAcc() "=" Expression() ";" {jjtThis.type = "[]="; jjtThis.identifier = t.image;}
		| LOOKAHEAD(2) t=<IDENTIFIER> "=" Expression() ";" {jjtThis.type = "="; jjtThis.identifier = t.image;}
		| Expression() ";"
	} catch(Exception e) {
		System.out.println("Expected Statement\n\n" + e);
		System.exit(1);
	}
}

void Expression() #void: {}
{
	try {
		LT() (LOOKAHEAD(2) "&&" LT() #AND(2))* 
	} catch(Exception e) {
		System.out.println("Expected Expression\n\n" + e);
		System.exit(1);
	}
}

void LT() #void: {}
{
	Sum() (LOOKAHEAD(2) "<" Sum() #LESS)* 
}

void Sum() #void: {}
{
	Mult() (LOOKAHEAD(2) ("+" Mult() #ADD | "-" Mult() #SUB))*
}

void Mult() #void: {}
{
	Literal() (LOOKAHEAD(2) ("*" Literal() #MULT | "/" Literal() #DIV))*
}

void Literal() : {Token t;}
{
  t=<INTEGERLITERAL> OtherLiteral() {jjtThis.identifier = t.image;}
	| "true" OtherLiteral() {jjtThis.identifier = "true";}
	| "false" OtherLiteral() {jjtThis.identifier = "false";}
	| t=<IDENTIFIER> OtherLiteral() {jjtThis.identifier = t.image;}
	| "this" OtherLiteral() {jjtThis.identifier = "this";}
	| "new" ((t="int" ArrayAcc() OtherLiteral() {jjtThis.identifier = "new int[...]";}) | (t=<IDENTIFIER> "(" ")" OtherLiteral() {jjtThis.identifier = "new " + t.image;}))
	| t="!" Expression() OtherLiteral() {jjtThis.identifier = "!";}
	| "(" Expression() ")" OtherLiteral()
}


void OtherLiteral() #void: {Token t;}
{
	ArrayAcc() OtherLiteral() {jjtThis.type = "[]";} #OtherLiteral
	| "." (("length" OtherLiteral() {jjtThis.type = "length";} #OtherLiteral) | (t=<IDENTIFIER> "(" [ MethodParams() ] ")" OtherLiteral() {jjtThis.type = "call"; jjtThis.identifier = t.image;} #OtherLiteral))
	| {}
}

void MethodParams(): {}
{
	Expression() ( "," Expression())*
}

void ArrayAcc() #void: {}
{
  "[" Expression() "]"	
}